buildscript {
    ext {
        protobufVersion = '3.25.3'
        grpcVersion = '1.58.0'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'org.rococo'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5'
}

protobuf {

    generatedFilesBaseDir = "$projectDir/build/generated"

    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {

                    outputSubDir = 'java'
                }
            }
            task.plugins {
                grpc {

                    outputSubDir = 'java'
                }
            }
        }
    }
}


clean {
    delete protobuf.generatedFilesBaseDir
}


sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs "$buildDir/generated/sources/proto/main/java"
        }
    }
}


idea {
    module {

        generatedSourceDirs = [file("$buildDir/generated/sources/proto/main/java")]
    }
}