plugins {
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.google.cloud.tools.jib' version '3.4.1'
}

group = 'org.rococo'
version = '1.0.0'

dependencies {
    implementation project(':rococo-grpc-common')
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    implementation "net.devh:grpc-server-spring-boot-starter:${project.ext.springGrpcVersion}"
    runtimeOnly "mysql:mysql-connector-java:${project.ext.mysqlDriverVersion}"

    annotationProcessor "org.projectlombok:lombok:${project.ext.lombokVersion}"
    compileOnly "org.projectlombok:lombok:${project.ext.lombokVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.grpc:grpc-testing:1.64.0'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    implementation 'org.glassfish:jakarta.el:5.0.0-M1'
}

jib {
    container {
        ports = ['8093']
        jvmFlags = ["-Dspring.profiles.active=${System.getenv("PROFILE")}"]
        creationTime = 'USE_CURRENT_TIMESTAMP'
        labels = [
                'version'   : "${project.version}".toString()
        ]
    }
    from {
        image = "${project.ext.dockerImage}"
        platforms {
            platform {
                architecture = "${project.ext.dockerArch}"
                os = 'linux'
            }
        }
    }
    to {
        image = "${project.ext.dockerHubName}/${project.name}-${System.getenv("PROFILE")}"
        tags = ['latest', "${project.version}"]
    }
}

tasks.jib.dependsOn test
tasks.jibDockerBuild.dependsOn test

tasks.named('test') {
    useJUnitPlatform()
}